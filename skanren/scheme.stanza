defpackage skanren:
    import core
    import collections
    import typeof

; mukanren commands
; `(var, var?, var=?)
; `(walk, ext-s, u=)
; `(unit, mzero)
; `(unify)
; `(call/fresh)
; `(conj, disj)
; `(mplus, bind)

; support functionality I probably need to implement it in 
; a similar way to the paper
; `(lambda, apply, define)
; `(print)

; TODO:: How to define aliases for types?
; deftype PrimitiveType = Int|String
; deftype Expression = List|PrimitiveType

; defn make-env ():
;     HashTable<StringSymbol, List|Int|String>()


; How to do type aliases?
; defstruct Environment <: HashTable<StringSymbol, List|Int|String>
deftype Environment <: Table<StringSymbol, True|False|List|Array|Int|String>
deftype EmptyEnvironment <: Environment
defmulti parent (env:Environment) -> Environment
defmulti set-direct (env:Environment, k:StringSymbol, v) -> False
defmulti to-env-list (env:Environment) -> List<HashTable<StringSymbol, True|False|List|Array|Int|String>>


public defn Environment (parent:Environment) -> Environment:
    val _parent:Environment = parent
    val env = HashTable<StringSymbol, True|False|List|Array|Int|String>()
    new Environment :
        ; Table Implementation Methods
        defmethod to-seq (this):
            match(parent):
                (p:EmptyEnvironment) : to-seq(env)
                (p:Environment) : cat(to-seq(env), to-seq(p))
        defmethod length (this):
            match(parent):
                (p:EmptyEnvironment) : length(env)
                (p:Environment) : length(p) + length(env)
        defmethod set (this, k:StringSymbol, v):
            println("set: %_ %_" % [k, v])
            match(find(key?{_, k}, to-env-list(this))):
                (f_env:Table) :
                    println("set: f_env = %@" % [f_env])
                    f_env[k] = v
                (f_env:False) :
                    println("set: f_env = false")
                    env[k] = v
                    println("set: f_env = false, setting in env = %@" % [env])
        defmethod get? (this, k:StringSymbol, d):
            match(find(key?{_, k}, to-env-list(this))):
                (f_env:Table) : f_env[k]
                (f_env:False) : d
        defmethod get (this, k:StringSymbol):
            println("to-env-list %@" % [to-env-list(this)])
            find!(key?{_, k}, to-env-list(this))[k]
        defmethod remove (this, k:StringSymbol):
            match(find(key?{_, k}, to-env-list(this))):
                (f_env:Table) : remove(f_env, k)
                (f_env:False) : false
        defmethod clear (this):
            clear(env)
            match(parent):
                (p:EmptyEnvironment) : false
                (p:Environment) : clear(p)

        ; Environment specific function calls
        defmethod parent (this) : _parent
        defmethod set-direct (this, k, v):
            env[k] = v
        defmethod to-env-list (this):
            match(_parent):
                (p:EmptyEnvironment) : List(env)
                (p:Environment) : cons(env, to-env-list(p))


public defn Environment () -> Environment:
    Environment(new EmptyEnvironment)


defn apply (lambda, args) -> True|False|List|Array|Int|String:
    println("apply: lambda = %_, args = %_" % [lambda, args])
    val arg_names = lambda[1]
    val body = lambda[2]
    val env = lambda[3]
    ; no laziness here, eval those args!
    val evaled-args = map({eval(env, _)}, args)
    ; extend environment
    val local_env = Environment(env)
    ; map({env[gensym(_)] = _}, arg_names, evaled-args)
    map({set-direct(local_env, _, _)}, arg_names, evaled-args)
    println("apply: env = %," % [local_env])
    println("apply: body = %_" % [body])
    ; eval in that environment
    val result = eval(local_env, body)
    println("apply: result = %_" % [result])
    result


defn eval-cond (env:Environment, L) -> True|False|List|Array|Int|String:
    ; Not handling else correctly here, also not sure about what correct behaviour is
    ; if nothing matches, do nothing or raise error?
    defn eval-item (v):
        if head(v) == `else :
            One(last(v))
        else:
            match(eval(env, head(v))):
                (r:True) : One(last(v))
                (r:False) : None()
    match(first(eval-item, L)):
        (v:One) : eval(env, value(v)) 
        (v:None) : false


defn eval-exp (env:Environment, s-exp) -> True|False|List|Array|Int|String:
    val L = s-exp
    ; curly braces are partial function evaluation
    switch {head(L) == _}:
        `+ : 
            val a = eval(env, L[1]) as Int
            val b = eval(env, L[2]) as Int
            val c:Int = a + b
            println("%_ + %_ = %_" % [a, b, c])
            c
        `define :
            println("define(%_, %_)" % [L[1], L[2]])
            ; I think we need to wrap everything in Maybes, since by adding booleans I have
            ; mixed up the meaning of false, in the first pass false wasn't in the language so 
            ; it meant an underlying language thing only, but now define returns false so it
            ; can work as part of a predicate which is weird
            env[L[1]] = eval(env, L[2])
        `lambda :
            println("lambda: %@" % [L])
            println("lambda-types: %@" % [seq(object-type, L)])
            append(L, List(env))
        `procedure? :
            println("procedure? %_" % [L[1]])
            match(eval(env, L[1])):
                (v:List) :
                    false when empty?(v) else head(v) == `lambda
                (v:True|False|Int|String) : false
        `let :
            println("let %@" % [tail(L)])
            val lambda = List(`lambda, map(head, L[1]), L[2], env)
            val args = map(last, L[1])
            apply(lambda, args)
        `cond :
            println("cond %@" % [tail(L)])
            eval-cond(env, tail(L))
        `= :
            println("= %@" % [tail(L)])
            eval(env, L[1]) == eval(env, L[2])
        `if :
            println("if %@" % [tail(L)])
            val cond-exp = List(List(L[1], L[2]), List(`else, L[3]))
            eval-cond(env, cond-exp)
        `and :
            println("and %@" % [tail(L)])
            var acc = true
            label<False> break:
                for v in tail(L) do:
                    acc = acc and (eval(env, v) as True|False)
                    if not acc: break(false)
            acc
        `cons :
            println("cons %@" % [tail(L)])
            cons(eval(env, L[1]), eval(env, L[2]) as List)
        `car :
            println("car %@" % [tail(L)])
            head(eval(env, L[1]) as List)
        `cdr :
            println("cdr %@" % [tail(L)])
            tail(eval(env, L[1]) as List)
        `null? :
            println("null? %@" % [tail(L)])
            eval(env, L[1]) == `()
        `vector :
            println("vector %@" % [tail(L)])
            to-array<True|False|List|Array|Int|String>(map({eval(env, _)}, tail(L)))
        `vector-ref :
            println("vector-ref %@" % [tail(L)])
            get(eval(env, L[1]) as Array, eval(env, L[2]) as Int)
        `@quote :
            println("quote %@" % [tail(L)])
            ; Not sure how to implement this? Can probably do without
            last(L)
        else :
            val lambda = eval(env, head(L))
            apply(lambda, tail(L))


public defn eval (env:Environment, s-exp) -> True|False|List|Array|Int|String:
    println("s-exp = %_, type = %_" % [s-exp, object-type(s-exp)])
    match(s-exp):
        (b:True|False) :
            println("Bool(%_)" % [b])
            b
        (i:Int) :
            println("Int(%_)" % [i])
            i
        (s:String) :
            println("String(%_)" % [s])
            s
        (x:StringSymbol) :
            println("%_ = %_" % [x, get?(env, x)])
            env[x]
        (L:List) :
            if empty?(L):
                L
            else:
                eval-exp(env, L)
